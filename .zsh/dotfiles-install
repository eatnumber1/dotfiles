#!/usr/bin/env zsh
emulate -L zsh
setopt err_exit no_unset
#setopt xtrace

echo "Updating submodules"
git submodule update --init --recursive

.zsh/syntax-check-dotfiles

echo "Fixing permissions on the .ssh directory"
zmodload -F zsh/parameter +p:commands
if (( $+commands[restorecon] )) restorecon -R .ssh
chmod go= .ssh
for f (.ssh/*) if [[ -f "$f" ]] chmod go-w "$f"

echo "Setting up authorized_keys2"
cp .ssh/.authorized_keys2 .ssh/authorized_keys2

if [[ $OSTYPE =~ darwin.+ ]]; then
  # GMacs have a custom ssh-add command that doesn't support -K. Use an
  # absolute path to bypass ssh-add on GMac.
  /usr/bin/ssh-add -K ~/.ssh/id_rsa
fi

echo "Installing Starship"
curl -sS https://starship.rs/install.sh | sh -s -- --bin-dir $HOME/bin --yes

echo "Installing bkt"
() {
  version="v0.5.4"
  architecture="$(uname -m)"

  case "$OSTYPE" in
    linux-gnu*)
      platform="linux-gnu"
      vendor="unknown"
      ;;
    darwin*)
      platform="darwin"
      vendor="apple"
      ;;
    *)
      echo "Unknown platform $OSTYPE, will not install bkt" >&2
      return
  esac

  ln -sf "$HOME/bin/bkt.${version}.${architecture}-${vendor}-${platform}" $HOME/bin/bkt
}
